# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/window/window_manager/windowmanager_aafwk.gni")

config("libwindow_extension_private_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "include/zidl",
    "../extension_connection/include",
    "../extension_connection/include/zidl",
    "../../interfaces/kits/napi/window_runtime/window_napi",
    "../../interfaces/innerkits/extension",
    "../../interfaces/innerkits/wm",
  ]
}

## Build libwindow_extension.so
ohos_shared_library("libwindow_extension") {
  sources = [
    "../extension_connection/src/zidl/window_extension_client_proxy.cpp",
    "src/js_window_extension.cpp",
    "src/js_window_extension_context.cpp",
    "src/window_extension.cpp",
    "src/window_extension_context.cpp",
    "src/window_extension_stub_impl.cpp",
    "src/zidl/window_extension_stub.cpp",
  ]

  configs = [
    ":libwindow_extension_private_config",
    "//foundation/window/window_manager/resources/config/build:coverage_flags",
  ]

  deps = [
    "${ability_runtime_napi_path}/inner/napi_common:napi_common",
    "${ability_runtime_path}/frameworks/native/appkit:app_context",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service_client:librender_service_client",
    "//foundation/window/window_manager/interfaces/kits/napi/window_runtime:window_native_kit",
    "//foundation/window/window_manager/wm:libwm",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_context_native",
    "ability_runtime:ability_manager",
    "ability_runtime:abilitykit_native",
    "ability_runtime:runtime",
    "c_utils:utils",
    "hilog_native:libhilog",
    "hitrace_native:hitrace_meter",
    "input:libmmi-client",
    "ipc:ipc_core",
  ]

  part_name = "window_manager"
  subsystem_name = "window"
}

config("window_extension_module_private_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "${ability_runtime_path}/interfaces/kits/native/ability/ability_runtime",
  ]
}

ohos_shared_library("window_extension_module") {
  sources = [ "src/window_extension_module_loader.cpp" ]

  configs = [
    ":window_extension_module_private_config",
    "//foundation/window/window_manager/resources/config/build:coverage_flags",
  ]

  deps = [ "//foundation/window/window_manager/extension/window_extension:libwindow_extension" ]

  external_deps = [
    "ability_runtime:abilitykit_native",
    "hiviewdfx_hilog_native:libhilog",
  ]
  relative_install_dir = "extensionability/"
  subsystem_name = "window"
  part_name = "window_manager"
}

group("test") {
  testonly = true
  deps = [ "test:test" ]
}
