# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

## Build libnative_image.so {{{
config("native_image_config") {
  visibility = [ ":*" ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
  ]
}

config("libnative_image_public_config") {
  include_dirs = [
    "//foundation/communication/ipc/interfaces/innerkits/ipc_core/include",
    "//foundation/graphic/graphic_2d/frameworks/surface/include",
    "//foundation/graphic/graphic_2d/utils/sandbox",
    "include",
  ]
}

ohos_shared_library("libnative_image") {
  sources = []
  if (surface_enable_gpu) {
    sources += [
      "src/native_image.cpp",
      "src/surface_image.cpp",
    ]

    if (defined(graphic_2d_ext_configs.vendor_root)) {
      sources += graphic_2d_ext_configs.libnative_image_ext_sources
      sources -= graphic_2d_ext_configs.libnative_image_ext_sources_del
    }
  }

  configs = [ ":native_image_config" ]

  public_configs = [ ":libnative_image_public_config" ]

  deps = [
    "//foundation/graphic/graphic_2d:libgl",
    "//foundation/graphic/graphic_2d/frameworks/surface:surface",
    "//foundation/graphic/graphic_2d/utils:sync_fence",
  ]

  output_extension = "so"

  external_deps = [
    "c_utils:utils",
    "hilog_native:libhilog",
  ]

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}
## Build libnative_image.so }}}
